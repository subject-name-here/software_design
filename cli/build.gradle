plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.11'
    id 'antlr'
    id 'application'
}

group 'ru.iisuslik'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ['-package', 'ru.iisuslik.cli', '-visitor']
    outputDirectory = new File("$buildDir/generated-src/antlr/main/ru/iisuslik/cli/")
}

compileJava.dependsOn generateGrammarSource
compileKotlin.dependsOn generateGrammarSource

sourceSets {
    main {
        java {
            srcDirs "$buildDir/generated-src/antlr/main/"
        }
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    testCompile group: 'junit', name: 'junit', version: '4.12'

    compile "org.antlr:antlr4-runtime:4.7.1"
    antlr "org.antlr:antlr4:4.7.1"
    compile "com.xenomachina:kotlin-argparser:2.0.+"
}

mainClassName = 'ru.iisuslik.cli.MainKt'

        compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jar {
    manifest {
        attributes(
                'Main-Class': 'ru.iisuslik.cli.MainKt'
        )
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}